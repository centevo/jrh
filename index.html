<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>CodePen - Font Experiment | WebGL</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<!-- partial:index.partial.html -->
<canvas id="canvas"></canvas>
<!-- VertexShader code here -->
<script id="vertexShader" type="x-shader/x-vertex">#version 300 es
precision highp float;
  in vec4 position;

  void main() {
    gl_Position = vec4( position );
  }
</script>

<!-- FragmentShader code here -->
<script id="fragmentShader" type="x-shader/x-fragment">#version 300 es
precision highp float;
out vec4 fragColor;
uniform vec2 u_resolution;
uniform float u_time;
uniform vec4 u_mouse;
  
#define R           u_resolution
#define T           u_time
#define M           u_mouse

#define PI          3.141592653
#define PI2         6.283185307

#define MIN_DIST    1e-4
#define MAX_DIST    55.

// constants
const float sz = .425;
const float hf = sz/2.;

const vec4 bz = vec4(.075,.055,.035,.0);
const vec4 bo = vec4(.175,.15,.075,.025);
const vec2 oa = vec2(.15,.1);

vec3 hue(float t){ return .55 + .45*cos(PI2*t*(vec3(.95,.97,.98)+vec3(1,.5,.2))); }
float hash21(vec2 p){return fract(sin(dot(p,vec2(27.619,57.583)))*4358.5453);}
mat2 rot(float a){return mat2(cos(a),sin(a),-sin(a),cos(a)); }

float box( in vec2 p, in vec2 b ){
    vec2 d = abs(p)-b;
    return length(max(d,0.))+min(max(d.x,d.y),0.);
}

float rbox( in vec2 p, in vec2 b, in vec4 r ) {
    r.xy = (p.x>0.)?r.xy : r.zw;
    r.x  = (p.y>0.)?r.x  : r.y;
    vec2 q = abs(p)-b+r.x;
    return min(max(q.x,q.y),0.)+length(max(q,0.))-r.x;
}
// special
float getdp(vec2 p) {
    float bt = length(p)-.075;
    return bt;
}
// number
float get3(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
    rbox(p-of,oa,bz.xxww),
    -rbox(p-of+vec2(.05,0),vec2(.15,.05),bz.zzww)
    );
    float bb = max(
    rbox(p+of,oa,bz.xxww),
    -rbox(p+of+vec2(.05,0),vec2(.15,.05),bz.zzww)
    );
    return min(bt,bb);
}
//letters
float getA(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
    rbox(p,vec2(.15,.175),bz.xwxw),
    -rbox(p+vec2(.0,.05),vec2(.1,.175),bz.zwzw)
    );
    float bb = box(p,vec2(.15,.025));
    return min(bt,bb);
}
float getB(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
        rbox(p-of,oa,bz.xxww),
        -rbox(p-of,vec2(.1,.05),bz.zzww)
    );
    float bb = max(
        rbox(p+of,oa,bz.xxww),
        -rbox(p+of,vec2(.1,.05),bz.zzww)
    );
    return min(bt,bb);
}
float getC(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),bz.wwxx),
        -rbox(p-vec2(.05,0),vec2(.15,.125),bz.wwzz)
    );
    return bt;
}
float getD(vec2 p) {
    float bt = max(rbox(p,vec2(.15,.175),bz.xxww),
    -rbox(p,vec2(.1,.125),vec4(.035,.035,0,0)));
    return bt;
}
float getE(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
        rbox(p-of,oa,bz.wwxx),
        -rbox(p-of-vec2(.05,0),vec2(.15,.05),bz.wwzz)
    );
    float bb = max(
        rbox(p+of,oa,bz.wwxx),
        -rbox(p+of-vec2(.05,0),vec2(.15,.05),bz.wwzz)
    );
    return min(bt,bb);
}
float getF(vec2 p) {
    float bt = min(
        box(vec2(p.x+.125,p.y),vec2(.025,.175)),
        box(vec2(p.x,abs(p.y-.075)-.075),vec2(.15,.025))
    );
    return bt;
}
float getG(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),bz.wwxx),
        -rbox(p-vec2(.05,0),vec2(.15,.125),bz.wwzz)
    );
    float bf = min(
        box(vec2(p.x-.125,p.y+.075),vec2(.025,.1)),
        box(vec2(p.x-.05,p.y),vec2(.1,.025))
    );
    return min(bt,bf);
}
float getH(vec2 p) {
    float bt = min(
        box(vec2(abs(p.x)-.125,p.y),vec2(.025,.175)),
        box(p,vec2(.15,.025))
    );
    return bt;
}
float getI(vec2 p) {
    float bt = min(
        box(p,vec2(.025,.175)),
        box(vec2(p.x,abs(p.y)-.15),vec2(.15,.025))
    );
    return bt;
}
float getJ(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),vec4(.0,.1,0,0)),
        -rbox(p+vec2(.05,-.05),vec2(.15,.175),vec4(.0,.055,0,0))
    );
    return bt;
}
float getK(vec2 p) {
    float bt = max(
        rbox(p-vec2(0,.075),oa,vec4(.0,.1,0,0)),
        -rbox(p-vec2(0,.12),vec2(.1,.095),vec4(.0,.055,0,0))
    );
    float bb = max(
        rbox(p+vec2(0,.075),oa,vec4(.1,.0,0,0)),
        -rbox(p+vec2(0,.12),vec2(.1,.095),vec4(.055,.0,0,0))
    );
    return min(bt,bb);
}
float getL(vec2 p) {
    float bt = min(
        box(p+vec2(.125,0),vec2(.025,.175)),
        box(p+vec2(0,.15),vec2(.15,.025))
    );
    return bt;
}
float getM(vec2 p) {
    vec2 of = vec2(.065,0);
    float bt = max(
        rbox(p+of,vec2(.085,.175),bz.xwxw),
        -rbox(p+of+vec2(.0,.05),vec2(.0375,.175),bz.zwzw)
    );
    float bb = max(
        rbox(p-of,vec2(.085,.175),bz.xwxw),
        -rbox(p-of+vec2(.0,.05),vec2(.0375,.175),bz.zwzw)
    );
    return min(bt,bb);
}
float getN(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),bz.xwxw),
        -rbox(p+vec2(.0,.045),vec2(.1,.175),bz.zwzw)
    );
    return bt;
}
float getO(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),bz.xxxx),
        -rbox(p,vec2(.1,.125),bz.zzzz)
    );
    return bt;
}
float getP(vec2 p) {
    float bt = max(
        rbox(p-vec2(.0,.075),oa,bz.xxww),
        -rbox(p-vec2(.0,.075),vec2(.1,.05),bz.zzww)
    );
    float bb = box(p+vec2(.125,0),vec2(.025,.175));
    return min(bt,bb);
}
float getQ(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),vec4(.075,.11,.075,.075)),
        -rbox(p,vec2(.1,.125),vec4(.035,.065,.035,.035))
    );
    vec2 q = (p+vec2(-.0925,.115))*rot(.8);
    float bb = box(q,vec2(.025,.065));
    return min(bt,bb);
}
float getR(vec2 p) {
    float bt = max(
        rbox(p-vec2(.0,.075),oa,bz.xxww),
        -rbox(p-vec2(.0,.075),vec2(.1,.05),bz.zzww)
    );
    float bb = max(
        rbox(p+vec2(.0,.075),oa,bz.xwww),
        -rbox(p+vec2(.0,.12),vec2(.1,.095),bz.zwww)
    );
    return min(bt,bb);
}
float getS(vec2 p) {
    float bt = max(
        rbox(p-vec2(.0,.075),oa,bz.wwxx),
        -rbox(p-vec2(.075,.075),vec2(.175,.05),bz.wwzz)
    );
    bt = max(bt,-box(p-vec2(.085,.045),vec2(.08,.08)));
    float bb = max(
        rbox(p+vec2(.0,.075),oa,bz.xxww),
        -rbox(p+vec2(.075,.075),vec2(.175,.05),bz.zzww)
    );
    bb = max(bb,-box(p+vec2(.085,.045),vec2(.08,.08)));
    return min(bt,bb);
}
float getT(vec2 p) {
    vec2 of = vec2(0,.075);
    return min(box(p-of-vec2(0,.075),vec2(.15,.025)),box(p,vec2(.025,.175)));
}
float getU(vec2 p) {
    float bt = max(
        rbox(p,vec2(.15,.175),bz.wxwx),
        -rbox(p-vec2(0,.045),vec2(.1,.175),bz.wzwz)
    );
    return bt;
}
float getV(vec2 p) {
    vec2 q = p;
    p.x=abs(p.x);
    p*=rot(-.24);
    float bt = max(
        rbox(p-vec2(0,.03),vec2(.1,.2),bz.wxwx),
        -rbox(p-vec2(-.0475,.05),vec2(.1,.175),bz.wzwz)
    );
    return max(box(q,vec2(.2,.175)),bt);
}
float getW(vec2 p) {
    vec2 of = vec2(.065,0);
    float bt = max(
        rbox(p+of,vec2(.085,.175),bz.wxwx),
        -rbox(p+of-vec2(.0,.05),vec2(.0375,.175),bz.wzwz)
    );
    float bb = max(
    rbox(p-of,vec2(.085,.175),vec4(0,.075,0,.075)),
    -rbox(p-of-vec2(.0,.05),vec2(.0375,.175),bz.wzwz)
    );
    return min(bt,bb);
}
float getX(vec2 p) {
    vec2 r = p;
    vec2 q = p*rot(.5707);
    p*=rot(-.5707);
    float bt = min(box(p,vec2(.025,.225)),box(q,vec2(.025,.225)));
    return max(box(r,vec2(.2,.175)),bt);
}
float getY(vec2 p) {
    vec2 of = vec2(0,.075);
    float bt = max(
        rbox(p-of,oa,bz.wxwx),
        -rbox(p-of-vec2(0,.05),vec2(.1),bz.wzwz)
    );
    return min(bt,box(p+of,vec2(.025,.1)));
}

float getZ(vec2 p) {
    vec2 q = p*rot(.86);
    float bt = min(
        box(p-vec2(-.0125,.15),vec2(.1375,.025)),
        box(p+vec2(-.0125,.15),vec2(.1375,.025))
    );
    bt = min(
        min(length(vec2(abs(q.x)-.198,q.y))-.025,bt),
        box(q,vec2(.195,.025))
    );
    return bt;
}

float getLetter(float md, vec2 q) {
    float d = 0.;
    if(md==0.) d= getA(q.xy);
    if(md==1.) d= getB(q.xy);
    if(md==2.) d= getC(q.xy);
    if(md==3.) d= getD(q.xy);
    if(md==4.) d= getE(q.xy);
    if(md==5.) d= getF(q.xy);
    if(md==6.) d= getG(q.xy);
    if(md==7.) d= getH(q.xy);
    if(md==8.) d= getI(q.xy);
    if(md==9.) d= getJ(q.xy);
    if(md==10.) d= getK(q.xy);
    if(md==11.) d= getL(q.xy);
    if(md==12.) d= getM(q.xy);
    if(md==13.) d= getN(q.xy);
    if(md==14.) d= getO(q.xy);
    if(md==15.) d= getP(q.xy);
    if(md==16.) d= getQ(q.xy);
    if(md==17.) d= getR(q.xy);
    if(md==18.) d= getS(q.xy);
    if(md==19.) d= getT(q.xy);
    if(md==20.) d= getU(q.xy);
    if(md==21.) d= getV(q.xy);
    if(md==22.) d= getW(q.xy);
    if(md==23.) d= getX(q.xy);
    if(md==24.) d= getY(q.xy);
    if(md==25.) d= getZ(q.xy);
    if(md==26.) d= getdp(q.xy);
    if(md==27.) d= getdp(q.xy);
    if(md==28.) d= getdp(q.xy);
    return d;
}

void main() { 
    vec2 F = gl_FragCoord.xy;

    vec3 C = vec3(0);
    vec2 uv = (F.xy - R.xy*.5)/R.y;
    vec2 xv = uv;

        uv *= rot(T*.075);
        uv = vec2(log(length(uv)), atan(uv.y, uv.x))*1.557;
        uv.x -= T*.15;
        //uv*=5.;uv+=T*.3;
    
        float px = fwidth(uv.x)*.8;

        vec2 q = mod(uv+hf,sz)-hf;
        vec2 id = floor((uv+hf)/sz);

        float hs = floor(hash21(id)*28.);

        float d = getLetter(hs,q.xy);

        hs = fract(hs*32.123);
        float nsd = 1.;

        vec3 clr = hs>.5? vec3(1):hue(hs*.1+(id.x*.075)-T*.1 );

        float ths = hash21(id+floor(T*.5)+hs);
        float mx = mod(floor(hs+T*6.),2.)*2.-1.;

        if(ths>.925) clr = mx>.5? vec3(0) : clr;
        if(ths>.075) C=mix(C,clr*nsd,smoothstep(px,-px,d));

        px = fwidth(xv.x)*.8;
        float c = length(xv)-.005;
        C = mix(C,C*.1,smoothstep(.13+px,-px,c));


    fragColor = vec4(pow(C, vec3(.4545)),1.);
}

</script>
<!-- partial -->
  <script src='https://jhancock532.github.io/twgl.min.js'></script><script  src="./script.js"></script>

</body>
</html>
